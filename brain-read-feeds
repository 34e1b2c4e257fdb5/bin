#!/usr/bin/env perl
use v5.14;
use FindBin;
use lib "$FindBin::Bin/lib";
use strict;
use YAML;
use Path::Class;
use WebService::Google::Reader;
use Getopt::Long;
use Try::Tiny;
use Brain;
use Ceis::Extractor;

my $urls = 0;
my $all = 0;

GetOptions("urls" => \$urls, "all" => \$all);

my $cred = YAML::LoadFile( file($ENV{HOME}, "etc", "password.yml" ) )->{'google-reader'};
my $reader = WebService::Google::Reader->new(%$cred);
my $feed = $reader->unread(count => 100);

my $brain = Brain->new;
my $extractor = Ceis::Extractor->new();

do {
    my @urls;
    my @entries = $feed->entries;

    for my $entry (@entries) {
        try { push @urls, $entry->link->href }
    }

    $reader->mark_read_entry(@entries);

    for my $url (@urls) {
        say "Studying $url";
        $extractor->url($url);

        my $fulltext = $extractor->fulltext;

        my $k_url      = $brain->blob->add($extractor->url);
        my $k_fulltext = $brain->study($fulltext);

        $brain->relation("origin")->add($k_fulltext, $k_url);
    }
} while ($reader->more($feed));
