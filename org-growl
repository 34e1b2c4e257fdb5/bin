#!/usr/bin/env perl

use v5.14;
use DateTime;
use Org::Parser;
use Growl::Any;

binmode STDOUT, ":utf8";

my @org_files = @ARGV;

die <<USAGE unless @org_files;

Usage: org-growl files1.org [ files2.org files3.org ... ]

USAGE

my $now = DateTime->now(time_zone => 'local');

my @scheduled;
for my $file (@org_files) {
    open my $fh, "<:utf8", $file;
    my $op = Org::Parser->new;
    my $doc = $op->parse($fh);

    $doc->walk(
        sub {
            my ($el) = @_;

            if ($el->isa("Org::Element::Headline") && $el->is_todo && ! $el->is_done) {
                for($el->find("Timestamp")) {
                    if ($_->field_name eq 'SCHEDULED') {

                        my $t = $_->datetime;
                        $t->set_time_zone($now->time_zone)
                            if $t->time_zone->name eq 'floating';

                        push @scheduled, [ $t, $el->title->as_string ];
                    }
                }
            }
        }
    );

    close $fh;
}

local $, = ", ";

my $growl = Growl::Any->new(appname => "org-growl", events => ["todo"]);
my $now_t = $now->epoch;

for(@scheduled) {
    my ($t, $title) = @$_;

    next unless $t > $now;

    if ( $t->epoch - $now_t < 900 ) {
        $growl->notify("todo", "@{[ $t->hms ]}", $title, "/Users/gugod/Icons/Things.png");
    }
}
