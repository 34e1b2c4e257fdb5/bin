#!/usr/bin/env perl

use v5.14;
use Org::Parser;
use Growl::Any;

binmode STDOUT, ":utf8";

my @org_files = @ARGV;

die <<USAGE unless @org_files;

Usage: org-growl files1.org [ files2.org files3.org ... ]

USAGE

my @scheduled;
for my $file (@org_files) {
    open my $fh, "<:utf8", $file;
    my $op = Org::Parser->new;
    my $doc = $op->parse($fh);

    $doc->walk(
        sub {
            my ($el) = @_;

            if ($el->isa("Org::Element::Headline") && $el->is_todo && ! $el->is_done) {
                for($el->find("Timestamp")) {
                    if ($_->field_name eq 'SCHEDULED') {
                        push @scheduled, [ $_->datetime, $el->title->as_string ];
                    }
                }
            }
        }
    );

    close $fh;
}


my $growl = Growl::Any->new(appname => "org-growl", events => ["todo"]);

my $now = DateTime->now;

for(@scheduled) {
    my ($t, $title) = @$_;
    next unless $t > $now;

    if ( $t->epoch - $now->epoch < 900 ) {
        $growl->notify("todo", "@{[ $t->hms ]}", $title, "/Users/gugod/Icons/Things.png");
    }
}
