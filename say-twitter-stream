#!/usr/bin/env perl
use common::sense;
use AnyEvent::Twitter::Stream;
use YAML;
use Regexp::Common qw /URI/;
use Data::RoundRobin;

my $keywords = join ",", @ARGV;

my $done = AnyEvent->condvar;

my $voice = Data::RoundRobin->new(qw(Alex Victoria Bruce Vicki Fred Kathy Ralph ));

my $twitter_config = YAML::LoadFile("$ENV{HOME}/.twitterrc");

my $listener = AnyEvent::Twitter::Stream->new(
    username => $twitter_config->{username},
    password => $twitter_config->{password},
    method   => "filter",
    track    => "$twitter_config->{username},$keywords",
    on_tweet => sub {
        my $tweet = shift;
        my $text = $tweet->{text};
        $text =~ s/$RE{URI}//;

        return unless $text =~ /^\p{IsASCII}+$/;
        say "$tweet->{user}{screen_name}:\n\n\t $tweet->{text}";
        system "/usr/bin/say -v $voice \Q$text\E";
        say "-" x 40;
    },
    on_keepalive => sub {
        warn "ping\n";
    },
    timeout => 45,
);

$done->recv;
