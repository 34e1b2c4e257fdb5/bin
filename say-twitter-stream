#!/usr/bin/env perl
use common::sense;
use AnyEvent;
use AnyEvent::Twitter::Stream;
use YAML;
use Regexp::Common qw /URI/;
use Data::RoundRobin;
use HTML::Entities;

sub random {
    $_[ rand(@_) ]
}

my $done = AnyEvent->condvar;

my $twitter_config = YAML::LoadFile("$ENV{HOME}/.twitterrc");

binmode STDOUT, ":utf8";

$SIG{INT} = sub {
    exit;
};
my $voice = "Alex";

my $listener = AnyEvent::Twitter::Stream->new(
    consumer_key => $twitter_config->{consumer_key},
    consumer_secret => $twitter_config->{consumer_secret},
    token    => $twitter_config->{access_token},
    token_secret => $twitter_config->{access_token_secret},
    method   => "userstream",
    on_tweet => sub {
        my $tweet = shift;
        my $text = $tweet->{text};

        next unless $text && $text =~ /\S/;

        say "$tweet->{user}{screen_name}:\n\n\t $tweet->{text}";
        say "-" x 40;

        $text =~ s/$RE{URI}//;
        $text = decode_entities($text);

        if ( @{[ $text =~ /@/g ]} > 4 ) {
            return;
        }

        if ($text =~ /^\p{ASCII}+$/) {
            $voice = random("Daniel", "Jill");
        }
        elsif ($text =~ /(\p{Hiragana}|\p{Katakana})/) {
            $voice = "Kyoko";
        } elsif ($text =~ /\p{Han}/) {
            $voice = "Ya-Ling";
        } else {
            return;
        }

        system "/usr/bin/say", -v => $voice, "$text";
    },
    on_keepalive => sub {
        warn "ping\n";
    },
    timeout => 600,
);

$done->recv;
