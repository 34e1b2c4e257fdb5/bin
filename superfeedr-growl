#!/usr/bin/env perl -w
use strict;
use warnings;
use 5.010;

package SuperfeedrGrowl;

my $AppName   = "Superfeedr-Growl";
my $EventType = "Notified";

sub notify {
    my ($class, $title, $summary) = @_;

    system(
        "growlnotify",
        '--name'  => $AppName,
        '--image' => "/Users/gugod/local/images/superfeedr.png",
        '-m' => $summary,
        '-t' => $title
    );
}

package main;
use AnyEvent::Superfeedr;
use String::Truncate qw(elide);
use YAML;
use Getopt::Std;

my $opts = {};
# config, username, password
getopts("c:u:p:", $opts);

if ($$opts{c}) {
    my $c = YAML::LoadFile($$opts{c});
    if (!$$c{jid} || !$$c{password}) {
        die "Config file should set jid and password.\n";
    }
    $$opts{u} = $$c{jid};
    $$opts{p} = $$c{password};
}
elsif (!$$opts{u} || !$$opts{p}) {
    die <<USAGE;

Usage:

    $0 -c <config_file>

Or

    $0 -u <jid> -p <password>

USAGE
}

SuperfeedrGrowl->notify("Superfeedr-Growl 程式啟動", "Program launched");

my $end = AnyEvent->condvar;
my $sf = AnyEvent::Superfeedr->new(
    jid => $$opts{u},
    password => $$opts{p},
    on_notification => sub {
        my $notification = shift;

        for my $entry ($notification->entries) {
            SuperfeedrGrowl->notify(
                elide($entry->title   || "(Untitled)" , 40),
                elide($entry->summary || "(No Sumary)", 140),
            );

            if ($$opts{d}) {
                my $dump = {};
                $dump->{$_} = $entry->$_ for qw(title author summary content link);
                say Dump $dump;
                say "----";
            }
        }
    },
);
$end->recv;
