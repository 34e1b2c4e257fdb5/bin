#!/usr/bin/env perl
use v5.14;
use utf8;
binmode STDIN, ":utf8";
binmode STDOUT, ":utf8";

my @urls;
@urls = @ARGV if @ARGV;

if ($urls[0] eq '-') {
    local $/ = undef;
    @urls = split /\n/, <STDIN>;
}

die <<USAGE unless @urls;

Pass URLs from argv:

   $0 URL URL...

Or pass URLs from STDIN:

   cat one-url-per-line.txt | $0 -

USAGE

use Find::Lib 'lib';
use Encode qw(encode_utf8 decode_utf8);
use Text::Autoformat;
use WWW::Mechanize;
use HTML::ExtractContent;
use HTML::WikiConverter::Markdown;

sub extract_as_markdown {
    state $co = HTML::WikiConverter::Markdown->new(dialect => "Markdown");
    state $ex = HTML::ExtractContent->new;
    return $co->html2wiki($ex->extract($_[0])->as_html);
}

my $ua = WWW::Mechanize->new(autocheck => 0);

for my $url (@urls) {
    $ua->get($url) or next;

    say autoformat extract_as_markdown $ua->content;
    say "-"x42;
}
