#!/usr/bin/env perl
use v5.14;
use File::Basename qw(basename);
use HTML::ExtractContent;
use HTML::WikiConverter::Markdown;
use Mojo::UserAgent;
use URI;

my $program_name = basename($0);

my $url = shift @ARGV or die <<USAGE;

Pass URLs from argv:

   $program_name URL

USAGE

sub extract_as_markdown {
    state $co = HTML::WikiConverter::Markdown->new(dialect => "Markdown");
    state $ex = HTML::ExtractContent->new;

    my $text = $co->html2wiki($ex->extract($_[0])->as_html);

    $text =~ s{<br(\s*/)>}{\n}g;

    return $text;
}

my $ua = Mojo::UserAgent->new;
my $tx = $ua->get($url);

my $base_url = URI->new($url);

my $dom = $tx->res->dom;
$dom->find("a, img")->each(
    sub {
        if ($_->type eq "a") {
            $_->attrs(href => "".URI->new_abs( $_->attrs("href"), $base_url ) );
        }
        else {
            $_->attrs(src  => "".URI->new_abs( $_->attrs("src"),  $base_url ) );
        }
    }
);

say extract_as_markdown $dom->root->html;

